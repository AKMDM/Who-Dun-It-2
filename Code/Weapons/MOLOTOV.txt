///Molotov (Throwing weapon) || Sprites by Lightman and Mooseknuckle, code by Theshooter7

Actor FakeMolly : CustomInventory 30076
{
  //$Category WDI Weapons
  //$Sprite MOLPA0
  //$Title "Molotov (Throwable)"
	Radius 16
	Height 8
  Scale 0.65
	Inventory.PickupMessage "Picked up a Molotov."
	Inventory.RespawnTics 21000 //10 Minutes
	States
	{
	Spawn:
		MOLP A -1
		Stop
	Pickup:
		MOLP A 0
		MOLP A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		MOLP A 0 A_PlaySound("pickups/weapon")
		MOLP A 0 A_GiveInventory("Molotov",1)
		MOLP A 0 A_GiveInventory("Slot1Filled",1)
		MOLP A 1 ACS_ExecuteAlways(670,0,25)
		Stop
	NotPickup:
		MOLP A 0
		MOLP A 1 ACS_ExecuteAlways(670,0,25)
		Fail
	}
}

Actor Molotov : Weapon
{
	Radius 8
	Height 32
    Scale 0.65
	Inventory.PickupMessage "Picked up a Molotov."
	Weapon.SelectionOrder 2200
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOAUTOAIM

	States
	{
	Spawn:
		MOLP A 1
		goto Death
	Pickup:

	Deselect:
		MLTV F 5
		MLTV F 0 A_TakeInventory("MolotovLit",1)
		MLTV F 0 A_PlaySound("weapons/lighterclose",CHAN_WEAPON)
		MLTV E 4
		MLTV DCB 1
		MLTV A 0 A_Lower
		MLTV A 1 A_Lower
		Goto Deselect+7

	Select:
		MLTV A 1 A_Raise
		Loop

	Ready:
		MOLO A 0 A_JumpIfInventory("MolotovLit",1,"IdleReady")
		Goto LightPre

	LightPre:
		MLTV BCD 1
		MLTV E 4
		MLTF E 0 A_PlaySound("weapons/lighteropen",CHAN_WEAPON)
		Goto Lighter
	Lighter:
		MLTV F 9
		MLTV G 0 A_JumpIfInventory("Flicked",3,"Success")
		MLTV G 0 A_Jump(64,"Success")
		MLTV G 0 A_PlaySound("weapons/lighter",CHAN_WEAPON)
		MLTV G 0 A_GiveInventory("Flicked",1)
		MLTV G 4
		Loop
	Success:
		MLTV G 0 A_TakeInventory("Flicked",3)
		MLTV G 0 A_GiveInventory("MolotovLit",1)
	    MLTV G 0 A_PlaySound("weapons/lighter",CHAN_WEAPON)
		MLTV G 2
		MLTV Z 2
		Goto IdleReady
	IdleReady:
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV I 2 bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV J 2 bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV K 2 bright A_WeaponReady
		Loop

	DropWeapon:
		MOLO A 0 A_TakeInventory("MolotovLit",1)
		MLTV A 0
		MOLO A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
       Goto Ready

	Fire:
	    MLTV L 2 bright
		MLTV MNM 2 bright
		MLTV O 0 A_TakeInventory("MolotovLit",1)
		MLTV O 0 A_PlaySound("weapons/lighterclose",CHAN_WEAPON)
		MLTV OPQR 1
		MLTV L 0 A_ReFire
		TNT1 A 2
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
        THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Molotov",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		MOLO A 0
		Goto Ready

        Hold:
		TNT1 A 2
		TNT1 A 1 A_Refire
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
        	THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Molotov",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		MOLO A 0
		Stop
	}
}

Actor ThrownMolly
{
  Radius 6
  Height 4
  Speed 20
  Damage 0
  Scale 0.45
  DamageType "friendlythrown"
  Projectile
  -NOGRAVITY
  -BOUNCEONActorS
  -BLOODSPLATTER
  -EXPLODEONWATER
  +NOBLOOD
  +SKYEXPLODE
  SeeSound "molly/flame"
  DeathSound "molly/burst"
  Obituary ""
  Gravity 0.85
  States
  {
  Spawn:
    MOPP ABCDEFGHIJKLMNOP 2 bright
    Loop

  Death:
    TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
    TNT1 A 0 A_Explode(10, 64, 1)
    TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MollyShard",1,0,Random(0,360), 2, Random(5, 45))
    TNT1 AAAAAAAAAAAAA 0 A_CustomMissile("MollyFireSpawn",1,0,Random(0,360), 2, Random(5, 30))
    TNT1 AAAA 0 A_CustomMissile("MollyFireSpawn3",1,0,Random(0,360), 2, Random(5, 30))
    TNT1 AAA 0 A_CustomMissile("MollyFireSpawn2",1,0,Random(0,360), 2, Random(30, 70))
    TNT1 A 1
    stop

  InWater:
    TNT1 A 1
    Stop
  }
}

Actor MollyFireSpawn
{
  Radius 3
  Height 4
  Speed 9
  Projectile
  Damage 0
  Scale 0.8
  Alpha 1.0
  Gravity 0.9
  Renderstyle Add
  DamageType "Burn"
  -NOGRAVITY
  -BLOODSPLATTER
  +RIPPER
  +FLOORHUGGER
  //+CEILINGHUGGER
  +DONTSPLASH
  +NOBLOOD
  +NODAMAGETHRUST
  States
  {
  Spawn:
    TNT1 A 6
    Goto Death

  Death:
    TNT1 A 0 A_Stop
    TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor MollyFireSpawn2 : MollyFireSpawn
{
  Speed 25
  Gravity 0.8
  -FLOORHUGGER
  States
  {
  Spawn:
    CFFX CDEFGHI 2 Bright
    Goto Death

  Death:
    TNT1 A 0 A_Stop
    TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor MollyFireSpawn3 : MollyFireSpawn
{
  States
  {
  Spawn:
    TNT1 A 3
    Goto Death

  Death:
    TNT1 A 0 A_Stop
    TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor MollyFire
{
  Radius 3
  Height 4
  Speed 8
  Projectile
  Damage 0
  Scale 0.8
  Alpha 0.0
  Gravity 1.5
  ReactionTime 60 // About 1 minute
  Renderstyle Add
  ActiveSound "molly/burn"
  DamageType "Burn"
  +BLOODLESSIMPACT
  -NOGRAVITY
  +DONTSPLASH
  +SLIDESONWALLS
  -BLOODSPLATTER
  +FLOORHUGGER
  //+CEILINGHUGGER
  +RIPPER
  +NOBLOOD
  +NODAMAGETHRUST
  States
  {
  Spawn:
    TNT1 A 0 
    TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
    TNT1 A 0 A_PlaySound("molly/burn", CHAN_VOICE, 1.0, 1)
    CFFX CDEFGHICDE 3 Bright A_FadeIn(0.1)
    Goto Ignited

  Ignited:
    TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
    TNT1 A 0 A_PlaySound("molly/burn", CHAN_VOICE, 1.0, 1)
    CFFX CDEFGHICDEF 3 Bright A_Explode(2, 64, 1)
    TNT1 A 0 A_CountDown
    Loop

  Death:
    TNT1 A 0 A_StopSound(CHAN_AUTO)
    CFFX CDEFGHICDEF 3 Bright A_FadeOut(0.1)
    Wait

  InWater:
    TNT1 A 1
    Stop
}}
 
Actor MollyShard
{
  Radius 6
  Height 4
  Speed 20
  Projectile
  Damage 0
  Bouncecount 2
  Scale 0.5
  Alpha 0.75
  Gravity 0.95
  Renderstyle Add
  +DONTSPLASH
  -NOGRAVITY
  +DOOMBOUNCE
  +RIPPER
  -BLOODSPLATTER
  +NOBLOOD
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_Jump(256, "Shard1", "Shard2", "Shard3", "Shard4")
    TNT1 A 1
    Stop

  Shard1:
    ICEC A 350
    ICEC A 35 A_FadeOut(0.05)
    Wait

  Shard2:
    ICEC B 350
    ICEC B 35 A_FadeOut(0.05)
    Wait

  Shard3:
    ICEC C 350
    ICEC C 35 A_FadeOut(0.05)
    Wait

  Shard4:
    ICEC D 350
    ICEC D 35 A_FadeOut(0.05)
    Wait

  Death: 
    TNT1 A 0 A_Jump(256, "Shard1", "Shard2", "Shard3", "Shard4")
    TNT1 A 1
    Stop
  }
} 
 
Actor DroppedMolly
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
  Scale 0.65
	Speed 9
	DeathSound "molly/land"
	States
	{
	Spawn:
		MOLP A 1
		Loop
	Death:
		MOLP A 35
		MOLP A 1 A_SpawnItemEx("FakeMolly",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor MolotovLit : Inventory
{
Inventory.MaxAmount 1
}

Actor Flicked : Inventory
{
Inventory.MaxAmount 3
}
