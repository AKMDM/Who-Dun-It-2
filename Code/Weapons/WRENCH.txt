//==================================================================
/////////////////////     WRENCH   /////////////////////////////////
//=================================================================
//Wrench (Wildweasel)
Actor FakeWrench : CustomInventory 30072
{
  //$Category WDI Weapons
  //$Sprite PIPPB0
  //$Title "Wrench"
	Radius 16
	Height 8
	Scale 0.5
	Inventory.PickupMessage "Found a heavy wrench." 
	Inventory.RespawnTics 3150 //1 minute 30 seconds
	States
	{
	Spawn:
		PIPP B -1
		Stop
	Pickup: 
		PIPP B 0 
		PIPP B 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		PIPP B 0 A_PlaySound("pickups/weapon")
		PIPP B 0 A_GiveInventory("PipeWrench",1)
		PIPP B 0 A_GiveInventory("Slot1Filled",1)
		PIPP B 1 ACS_ExecuteAlways(670,0,11)
		Stop
	NotPickup:
		PIPP B 0
		PIPP B 1 ACS_ExecuteAlways(670,0,11)
		Fail					  
}}

Actor PipeWrench : Weapon 
{
	SpawnID 32
	Radius 20
	Height 12
	Inventory.PickupMessage "Found a heavy wrench."
	Weapon.SelectionOrder 2200
        Weapon.preferredSkin "WrenchSkin"
	+WEAPON.MeleeWeapon
	+WEAPON.NoAutoFire
	+INVENTORY.UNDROPPABLE
   	+NOAUTOAIM
	States
	{
	Spawn:
		PIPP B 0
		goto Death
	Pickup: 
	Select:
		PIPW A 1 A_Raise
		Loop
	Deselect:
		PIPW A 1 A_Lower
		Loop
	Ready:
		PIPE A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		PIPW A 1 A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0
		PIPW A 1
		TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
       		Goto Ready
	Fire:
		PIPW A 0 A_JumpIfInventory("PipeSeq",2,"Fire3")
		PIPW A 0 A_JumpIfInventory("PipeSeq",1,"Fire2")
		PIPW ABC 1
		TNT1 A 1 A_ReFire
		TNT1 A 10 A_GiveInventory("PipeSeq",1)
		PIPW DEF 1
		PIPW G 1 A_CustomPunch(25,1,0,"WrenchPuff", 72)
		PIPW HI 1
		TNT1 A 4
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady
		PIPW C 1 A_TakeInventory("PipeSeq",999)
		PIPW BA 1
		Goto Ready
	Hold:
		TNT1 A 1
		TNT1 A 1 A_ReFire
		TNT1 A 10 A_GiveInventory("PipeSeq",1)
		PIPW DEF 1
		PIPW G 1 A_CustomPunch(25,1,0,"WrenchPuff", 72)
		PIPW HI 1
		TNT1 A 4
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady
		PIPW C 1 A_TakeInventory("PipeSeq",999)
		PIPW BA 1
		Goto Ready
	Fire2:
		TNT1 A 10 A_GiveInventory("PipeSeq",1)
		PIPW JKL 1
		PIPW M 1 A_CustomPunch(25,1,0,"WrenchPuff", 72)
		PIPW NO 1
		TNT1 A 8
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady
		PIPW C 1 A_TakeInventory("PipeSeq",999)
		PIPW BA 1
		Goto Ready
	Fire3:
		TNT1 A 10 A_TakeInventory("PipeSeq",1)
		PIPW DEF 1
		PIPW G 1 A_CustomPunch(35,1,0,"WrenchPuff", 72)
		PIPW HI 1
		TNT1 A 12
		PIPW C 1 A_TakeInventory("PipeSeq",999)
		PIPW BA 1
		Goto Ready
	Death:
	   	DHSP A 0 A_SpawnItem("DroppedWrench",0,0,0,0)
	   	TNT1 A 0 A_FadeOut(1.0)
	   	goto death + 1
}}

Actor DroppedWrench
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	Scale 0.5
	DeathSound "wrench/hitwall"
	States
	{
	Spawn:
		PIPP B 1
		Loop
	Death:
		PIPP B 35
		PIPP B 1 A_SpawnItemEx("FakeWrench",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor WrenchPuff
{
  DamageType "friendlymelee"
  +NOBLOCKMAP
  +NOGRAVITY
  +PUFFONActorS
  activesound "wrench/swing"
  attacksound "wrench/hitwall"
  seesound "wrench/hitbody"
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    Stop
  Crash:
  	TNT1 A 0
  	TNT1 A 1
  	Stop
  Melee:
  	TNT1 A 0
  	TNT1 A 1
  	Stop
}}
