//Shovel || Sprites and Sounds by Marty Kirra, code by Firewolf

Actor FakeShovel : CustomInventory 30074
{
  //$Category WDI Weapons
  //$Sprite SHOVA0
  //$Title "Shovel"
	Radius 16
	Height 8
	Scale 0.48
	Inventory.PickupMessage "Found a Shovel. Classy!"
	Inventory.RespawnTics 10500 //5 Minutes
	States
	{
	Spawn:
		SHOV A -1
		stop
	Pickup: 
		SHOV A 0 
		SHOV A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		SHOV A 0 A_PlaySound("pickups/weapon")
		SHOV A 0 A_GiveInventory("Shovel",1)
		SHOV A 0 A_GiveInventory("Slot1Filled",1)
		SHOV A 1 ACS_ExecuteAlways(670,0,14)
		Stop
	NotPickup:
		SHOV A 0
		SHOV A 1 ACS_ExecuteAlways(670,0,14)
		Fail						  
	}
}

Actor Shovel : Weapon
{
	Radius 8
	Height 32
	Scale 0.48
	Inventory.PickupMessage "Found a Shovel. Classy!"
	Weapon.preferredSkin "ShovelSkin"
	Weapon.SelectionOrder 2200
	+WEAPON.MELEEWEAPON
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
   +NOAUTOAIM
	
	States
	{
	Spawn:
		SHOV A 0
		goto Death
	Pickup: 
	
	Select:
		SHLR ABC 2
		goto Ready
		
	Deselect:
		SHLG A 1 A_Lower
		loop
		
	Ready:
		SHLG A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		SHLG A 1 A_WeaponReady
		Loop
		
	DropWeapon:
		SHLG A 0
		SHLG A 1
		SHLG A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
       Goto Ready
		
	Fire:
		SHLR CBA 2
		TNT1 A 3
		TNT1 A 1 A_Refire
        SHLR ABC 1
		SHLG ABC 1
		SHLG DE 1 A_CustomPunch(19, 1, 1, "ShovelPuff", 72)
		SHLG FGHI 1
		TNT1 A 16
		SHLR ABC 2
		Goto Ready

        Hold:
	    TNT1 A 1
		TNT1 A 1 A_Refire
        SHLR ABC 1
		SHLG ABC 1
		SHLG DE 1 A_CustomPunch(16, 1, 1, "ShovelPuff", 72)
		SHLG FGHI 1
		TNT1 A 16
		SHLR ABC 2
		Goto Ready
		
	Death:
	   POOL A 0 A_SpawnItem("DroppedShovel",0,0,0,0)
	   TNT1 A 0 A_FadeOut(1.0)
	   goto death + 1
	}
}

Actor DroppedShovel
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	Scale 0.48
	DeathSound "shovel/wall"
	States
	{
	Spawn:
		SHOV A 1
		Loop
	Death:
		SHOV A 35
		SHOV A 1 A_SpawnItemEx("FakeShovel",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor ShovelPuff
{
  DamageType "ShovelMelee"
  +NOBLOCKMAP
  +NOGRAVITY
  +PUFFONActorS
  activesound "shovel/swing"
  attacksound "shovel/wall"
  seesound "shovel/hit"
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    stop
  Crash:
  	TNT1 A 0
  	TNT1 A 1
  	stop
  Melee:
  	TNT1 A 0
  	TNT1 A 1
  	stop
  }
}
