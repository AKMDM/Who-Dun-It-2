//============================================
//Shotgun
//Actor ShotgunSpawn : Shotgun2 {}

ACTOR ShotgunOnce : Inventory
{
	Inventory.MaxAmount 1
	-INVBAR
}

ACTOR FakeShotgun : CustomInventory Replaces Shotgun 30050
{
  //$Category WDI Weapons
  //$Sprite DHSPA0
  //$Title "Shotgun"
	Radius 16
	Height 8
	Inventory.PickupMessage "Found a Shotgun."
	Inventory.RespawnTics 3024000 //1 hour
	//Scale 0.35
	States
	{
	Spawn:
		DHSP A -1
		Stop
	Pickup:
		DHSP A 0
		PIST A 0 A_JumpIfInventory("MurderMarker",1, 2)
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0
		DHSP A 0 A_JumpIfInventory("Shotgun2",1,"AmmoPickup")		
		DHSP A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		DHSP A 0 A_PlaySound("pickups/weapon")
		DHSP A 0 A_GiveInventory("Shotgun2",1)
		DHSP A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Shotgun.")
		DHSP A 1 ACS_ExecuteAlways(670,0,21)
		DHSP A 0 A_JumpIfInventory("ShotgunOnce",1,"ForgetIt")
		DHSP A 0 A_GiveInventory("SuperShells", 1)
		DHSP A 0 A_GiveInventory("ShotgunOnce",1)
		Stop
	NotPickup:
		DHSP A 0
		DHSP A 1 ACS_ExecuteAlways(670,0,21)
		Fail
	AmmoPickup:
		DHSP A 0
		DHSP A 0 //A_JumpIfInventory("NewShell",5,"ForgetIt")
		DHSP A 0 A_PlaySound("pickups/ammo")
		TNT1 A 0 //A_Print("Picked up some Shotgun ammo.")
		DHSP A 1 A_GiveInventory("NewShell", 1)
		Stop
	ForgetIt:
		DHSP A 1
		Stop
	}
}

Actor TKFakeShotgun : FakeShotgun
{
states
	{
		Pickup:
		DHSP A 0
		DHSP A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		DHSP A 0 A_JumpIfInventory("Shotgun2",1,"AmmoPickup")		
		DHSP A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		DHSP A 0 A_PlaySound("pickups/weapon")
		DHSP A 0 A_GiveInventory("Shotgun2",1)
		DHSP A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Shotgun.")
		DHSP A 1 ACS_ExecuteAlways(670,0,21)
		DHSP A 0 A_JumpIfInventory("ShotgunOnce",1,"ForgetIt")
		DHSP A 0 A_GiveInventory("SuperShells",1)
		DHSP A 0 A_GiveInventory("ShotgunOnce",1)
		Stop
	NotPickup:
		DHSP A 0
		DHSP A 1 ACS_ExecuteAlways(670,0,21)
		Fail
	 
	}
}

ACTOR DroppedShotgun
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		DHSP A 1
		Loop
	Death:
		DHSP A 35
		DHSP A 1 A_SpawnItemEx("FakeShotgun",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}











Actor Shotgun2 : Weapon
 {
    Weapon.SelectionOrder 3000
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0
    Weapon.Ammouse 1
    Weapon.Ammouse2 1
    Weapon.Ammotype1 "SuperShells"
    Weapon.Ammotype2 "NewShell"
	Weapon.preferredSkin "ShotgunSkin"
	+AMMO_OPTIONAL
	+ALT_AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    +NOAUTOAIM
    Obituary "%o caught some buck."
    Inventory.Pickupmessage "Found a Shotgun."
    AttackSound "weapons/Shotgunfire"

    States
    {
	Spawn:
       DHSP A 1
       goto Death
    Ready:
	   DHSG A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
       NSHT A 1 A_WeaponReady
		Loop
	  WeaponDrop:
		TNT1 A 0
		NSHT A 1
		TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
       Goto Ready
    Deselect:
       DHSG A 0 A_Playsound ("weapons/weapondown")
       DHSG A 0 A_Lower
       NSHT A 1 A_Lower
       goto deselect+1
    Select:
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
       NSHT A 1 A_Raise
       DHSG A 0 A_Raise
	   Goto Cock

	Cock:
       NSHT DEFGHI 1
	   NSHT J 2
	   NSHT K 1
	   NSHT K 0 A_PlaySound("HTSGBACK", CHAN_WEAPON, 2.0)
	   NSHT L 2
	   NSHT M 4
	   NSHT L 0 A_PlaySound("HTSGFWD", CHAN_WEAPON, 2.0)
	   NSHT L 2
	   NSHT K 1
	   NSHT J 2
	   NSHT IHGFED 1
       Goto Ready

    Fire:
       DHSG A 0 A_JumpIfNoAmmo ("EmptyFire")
	   DHSG A 0 A_FireBullets (5, 5, 8, 4, "FFBulletPuff")
	   DHSG A 0 A_Recoil(2)
	   TNT1 A 0 A_SetPitch(Random(-2,-1)+pitch)
	   TNT1 A 0 A_SetAngle(Random(-2,2)+angle)
       NSHT BCB 1
	   NSHT A 6

       NSHT DEFGHI 1
	   NSHT J 2
	   NSHT K 1
	   NSHT K 0 A_PlaySound("HTSGBACK", CHAN_WEAPON, 2.0)
	   NSHT L 2
	   NSHT M 4 A_FireCustomMissile ("ShotgunCasing",-70+random(-8,8),0,1,1)
	   NSHT L 0 A_PlaySound("HTSGFWD", CHAN_WEAPON, 2.0)
	   NSHT L 2
	   NSHT K 1
	   NSHT J 2
	   NSHT IHGFED 1

       NSHT A 2
       DHSG A 0
       Goto Ready
    EmptyFire:
       DHSG A 0 A_PlaySound ("weapons/sgclick")
       NSHT A 20 A_WeaponReady(WRF_NOFIRE)
       DHSG A 0
       Goto Ready
    AltFire:
	   NSHR A 0 A_JumpIfInventory("SuperShells",4,"Ready")
	   NSHR A 0 A_JumpIfInventory("NewShell",1,1)
	   Goto Ready
	   NSHR ABCDEFGHIJ 1
	   NSHR K 6
	   Goto Insert

	Insert:
	   NSHR A 0 A_JumpIfInventory("SuperShells",4,"EndReload")
	   NSHR A 0 A_JumpIfInventory("NewShell",1,1)
	   Goto EndReload
	   NSHR LMNO 1
	   NSHR P 0 A_PlaySound("HTSGSHEL",CHAN_WEAPON)
	   NSHR P 0 A_TakeInventory("NewShell",1)
	   NSHR P 0 A_GiveInventory("SuperShells",1)
	   NSHR P 2
	   NSHR Q 3
	   NSHR R 2
	   NSHR K 9
	   Loop

	   EndReload:
	   NSHR S 1
	   NSHR S 0 A_PlaySound("HTSGBACK",CHAN_WEAPON)
	   NSHR T 2
	   NSHR U 4
	   NSHR U 0 A_PlaySound("HTSGFWD", CHAN_WEAPON)
	   NSHR T 2
	   NSHR S 1
	   NSHR K 4

	   NSHR JIHGFEDCBA 1
	   Goto Ready

    Flash:
       DHSF A 3 BRIGHT
       DHSF A 0 A_Recoil(2)
       DHSF B 4 BRIGHT
       stop
	Death:
	   DHSP A 0 A_SpawnItem("DroppedShotgun",0,0,0,0)
	   TNT1 A 0 A_FadeOut(1.0)
	   goto death + 1
	}
}

ACTOR SuperShells : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 4
Inventory.Icon "DHSPA0"
+INVENTORY.UNDROPPABLE
States
{
Spawn:
TNT1 A -1
LOOP
}
}    

//Actor ShellSpawn : NewShell {}

ACTOR NewShell : Ammo 30061
{
  //$Category WDI Ammo
  //$Sprite D3S1E0
  //$Title "Shotgun Shell"
  Game Doom
  SpawnID 12
  Scale 0.2
  Inventory.PickupMessage "Found a shotgun shell." 
  Inventory.Amount 1
  Inventory.MaxAmount 4
  Inventory.RespawnTics 21000 //10 minutes 
  Ammo.BackpackAmount 1
  Ammo.BackpackMaxAmount 4
  +INVENTORY.INVBAR 
  -INVENTORY.INTERHUBSTRIP
  Inventory.Icon "ARTISHEL"
  States
  {
  Spawn: 
    D3S1 E -1
    Stop
  }
}