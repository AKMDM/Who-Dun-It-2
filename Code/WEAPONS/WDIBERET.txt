ACTOR FakeWDIBeretta : CustomInventory 26906
{
  //$Category WDI Weapons
  //$Sprite PISTA0
  //$Title "WDIBeretta" //(APOCALYPTO)
	Radius 16
	Height 8
	Inventory.PickupMessage "Found an old Beretta."
	Inventory.RespawnTics 3024000 
 	Scale 0.7
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("MurderMarker",1, 2)
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0
		PIST A 0 A_JumpIfInventory("WDIBeretta",1,"AmmoPickup")		
		PIST A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		PIST A 0 A_PlaySound("pickups/weapon")
		PIST A 0 A_GiveInventory("WDIBeretta",1)
		PIST A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		PIST A 0 A_JumpIfInventory("PistolOnce",1,"ForgetIt")
		PIST A 0 A_GiveInventory("PistolAmmo",random(1,8))
		PIST A 0 A_GiveInventory("PistolOnce",1)
		Stop
	NotPickup:
		PIST A 0
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		Fail
	AmmoPickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("PistolAmmo",7,"ForgetIt")
		PIST A 0 A_PlaySound("pickups/ammo")
		TNT1 A 0 
		PIST A 1 A_GiveInventory("PistolAmmo",random(1,8))
		Stop
	ForgetIt:
		LUGP A 1
		Stop
	}
}

Actor WDIBeretta : Weapon  
{
  Scale 0.7
  Obituary ""
  Radius 20
  Height 16
  AttackSound "weapons/BerettaFire"
  Inventory.pickupmessage "Picked up an old Beretta"
  Weapon.preferredSkin "PistolSkin"
  Weapon.SelectionOrder 400
  Weapon.SlotNumber 1
  Weapon.kickback 100
  Weapon.ammotype "PistolAmmo"
  Weapon.ammotype2 "PistolClip"
  Weapon.ammouse 1 
  +AMMO_OPTIONAL
  +ALT_AMMO_OPTIONAL
  +INVENTORY.UNDROPPABLE
  +NOAUTOAIM
  States
  {
  Spawn:
    PIST A -1
    Loop

  Ready:
    2PIS A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
    2PIS A 0 A_JumpIfNoAmmo("EmptyReady")
    2PIS A 1 A_WeaponReady
    goto Ready

  EmptyReady:
     2PIS A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
     2PIS C 3 A_WeaponReady
     goto EmptyReady

  EmptyLul:
    2PIS C 1 A_PlaySound("weapons/lugerempty")
    2PIS C 11 
     goto EmptyReady

  Deselect:
    2PIS D 1 A_Lower
    Loop

  Select:
    2PIS A 1 A_Raise
    Loop

  Fire:
    2PIS A 0 A_JumpIfNoAmmo ("EmptyLul")  
    2PIS A 4 A_Jumpifcloser(120,"Closeshot")   
    2PIS B 2 bright A_FireBullets(1.75, 1.75, -1, 11, "FFBulletPuff")
    2PIS B 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8, 12, 0, 0) 
    2PIS B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 10, 0, 0) 
    2PIS C 2 A_JumpIfNoAmmo ("EmptyLul")
    2PIS D 2
    2PIS A 2
    2PIS F 0 A_ReFire
    2PIS A 4 A_ReFire
    Goto Ready
	
   Closeshot:  
    2PIS B 2 bright A_FireBullets(1.70, 1.70, -1, 9, "FFBulletPuff")
    2PIS B 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8, 12, 0, 0) 
    2PIS B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 10, 0, 0) 
    2PIS C 2 A_JumpIfNoAmmo ("EmptyLul")
    2PIS D 2
    2PIS A 2
    2PIS F 0 A_ReFire
    2PIS A 4 A_ReFire
    Goto Ready	 
 
  Reload:
    2PIS C 0 A_JumpIfInventory("PistolAmmo", 10, "Ready")
    2PIS C 0 A_TakeInventory("PistolClip", 1)
    2PIS C 0 A_PlaySound("weapons/Lugerout")
    2PIS FGHIJK 2
    2PIS K 0 A_FireCustomMissile ("SpentClip5", 0, 0, 8, 12, 0, 0) 
    2PIS K 24
    2PIS K 0 A_PlaySound("weapons/Lugerin")
    2PIS LMNOPA 2
    2PIS Q 0 A_GiveInventory("PistolAmmo", 10)
    goto Ready 

  AltFire:
    goto AltFire2

  AltFire2:
    2PIS A 0 A_JumpIfInventory("PistolClip", 1, "Reload")
    2PIS A 0 A_JumpIfNoAmmo(1)
    goto Ready
    goto EmptyReady
 
  WeaponDrop:
    TNT1 A 0
		2PIS A 1
    TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(667,0,0,0,0)
       Goto Ready
  }
}

ACTOR DroppedWDIBeretta
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
  	Scale 0.7
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		PIST A -1
		Stop
	Death:
		PIST A 35
		PIST A 1 A_SpawnItemEx("FakeWDIBeretta",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor Dropper_WDIBeretta : DeathDropper
{
 States
 {
   Spawn:
	TNT1 A 1
	Wait

   Use:
	TNT1 A 1 A_FireCustomMissile("DroppedWDIBeretta", 0, 0, 0)
	Stop
 }
}

