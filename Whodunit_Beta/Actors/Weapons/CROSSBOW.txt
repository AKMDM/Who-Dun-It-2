//==================================================================
///////////////////      CROSSBOW       ////////////////////////////
//=================================================================
//Crossbow (Sprites by Mooseknuckle,Decorate by prepper)
Actor FakeCrossbow : CustomInventory 30055
{
	//$Category WDI Weapons
	//$Sprite XBPUA0
	//$Title "Crossbow"
	Radius 16
	Height 8
	Inventory.PickupMessage "Found a crossbow."
	Inventory.Respawntics 3024000 //1 hour
	//Scale 0.35
	States
	{
	Spawn:
		XBPU A -1
		Stop
	Pickup:
		XBPU A 0
		PIST A 0 A_JumpIfInventory("MurderMarker",1, 2)
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0
		XBPU A 0 A_JumpIfInventory("LCrossbow",1,"AmmoPickup")
		XBPU A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		XBPU A 0 A_PlaySound("Pickups/Weapon")
		XBPU A 0 A_GiveInventory("LCrossbow",1)
		XBPU A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Crossbow.")
		XBPU A 1 ACS_NamedExecute("OldWeapon_ID",0,5)
		XBPU A 0 A_JumpIfInventory("CrossbowOnce",1,"ForgetIt")
		XBPU A 0 A_GiveInventory("LoadedBolts",random(1,3))
		XBPU A 0 A_GiveInventory("CrossbowOnce",1)
		Stop
	NotPickup:
		XBPU A 0
		XBPU A 1 ACS_NamedExecute("OldWeapon_ID",0,5)
		Fail
	AmmoPickup:
		XBPU A 0
		XBPU A 0 //A_JumpIfInventory("Bolts",2,"ForgetIt")
		XBPU A 0 A_PlaySound("Pickups/Ammo")
		TNT1 A 0 //A_Print("Picked up some Crossbow Bolts.")
		XBPU A 1 A_GiveInventory("LoadedBolts",random(1,3))
		Stop
	ForgetIt:
		XBPU A 1
		Stop
}}

Actor TKFakeCrossbow : FakeCrossbow
{
	States
	{
	Pickup:
		XBPU A 0
		XBPU A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		XBPU A 0 A_JumpIfInventory("LCrossbow",1,"AmmoPickup")
		XBPU A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		XBPU A 0 A_PlaySound("Pickups/Weapon")
		XBPU A 0 A_GiveInventory("LCrossbow",1)
		XBPU A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Crossbow.")
		XBPU A 1 ACS_NamedExecute("OldWeapon_ID",0,5)
		XBPU A 0 A_JumpIfInventory("CrossbowOnce",1,"ForgetIt")
		XBPU A 0 A_GiveInventory("LoadedBolts",random(1,3))
		XBPU A 0 A_GiveInventory("CrossbowOnce",1)
		Stop
	NotPickup:
		XBPU A 0
		XBPU A 1 ACS_NamedExecute("OldWeapon_ID",0,5)
		Fail
}}

Actor LCrossbow: Weapon  //NOTE!! NOT DROP  due to duplicated Use in hex (renamed to LCrossbow)
{
	Obituary "%o got a new piercing."
	Weapon.SelectionOrder 3000
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType1 "LoadedBolts"
	//Weapon.AmmoType2 "Bolt"
	//Weapon.PreferredSkin ""
	Inventory.PickupMessage "Found a Repeating Crossbow"
	//AttackSound "Weapons/Shotgunfire"
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INTERHUBSTRIP
	+NOAUTOAIM
	States
	{
	Spawn:
		XBPU A  -1
		Goto Death
	Select:
		TNT1 A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_JumpIfNoAmmo(7)
		CBOW KJIHGF 1 //CON BOLT
		Goto Deselected
		CBOW BCDEF 1
		Goto Deselected
	Deselected:
		TNT1 A 1 A_PlaySound ("Weapons/Weapondown")
		TNT1 A 1 A_Lower
		Goto Deselected +1
	Ready:
		CBOW F 1 A_JumpIfInventory("LoadedBolts",1,6)
		CBOW EDCB 1
		CBOW B 1 A_JumpIfNoAmmo("FakeReady")
		CBOW GHIJK 1
		CBOW A 1
		Goto RealReady
	FakeReady:
		CBOW B 0 A_JumpIfInventory("LoadedBolts",1,"RealReady")
		CBOW B 1 A_WeaponReady
		CBOW B 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		Loop
	RealReady:
		CBOW A 1 A_WeaponReady
		CBOW A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		Loop
	WeaponDrop:
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
		Goto Ready
	Fire:
		CBOW A 0 A_JumpIfNoAmmo("FakeReady")
		CBOW B 0 A_CheckReload
		CBOW B 3 A_FireCustomMissile ("FlyingBolt",0,1,1,12)//FPF_TRANSFERTRANSLATION) znd3.0
		CBOW B 0 A_Recoil(1)
		CBOW C 2 A_PlaySound("Weapons/Crossbowfire")
		CBOW CD 4
		CBOW E 8
		CBOW FGHIJK 2
		CBOW A 5  A_JumpIfNoAmmo("FakeReady")
		Goto RealReady
	Death:
		DHSP A 0 A_SpawnItem("DroppedCrossbow",0,0,0,0)
		TNT1 A 0 A_FadeOut(1.0)
		Goto Death + 1
}}

Actor DroppedCrossbow
{
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "Weapons/Gundrop"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		XBPU A -1
		Stop
	Death:
		XBPU A 35
		XBPU A 1 A_SpawnItemEx("FakeCrossbow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor CrossbowOnce : Inventory
{
	Inventory.MaxAmount 1
	-INVBAR
}

Actor FlyingBolt : FastProjectile
{
	Radius 3
	Height 0
	Speed 180
	Damage 4
	DamageType "Friendlygun"
	Projectile
	-NOGRAVITY
	+STRIFEDAMAGE
	+RIPPER
	SeeSound ""
	DeathSound ""
	Obituary ""
	States
	{
	Spawn:
		ARWP A 1 A_LowGravity
		ARWP A 2 A_NoGravity
		Loop
	Death:
		ARWP A 0 A_SpawnItemEx("LoadedBolts2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor LoadedBolts : Ammo 30065
{
	//$Category WDI Ammo
	//$Sprite ARWPA7A3
	//$Title "Crossbow Bolt"
	Game Doom
	SpawnID 12
	Inventory.PickupMessage "Found a Crossbow bolt"
	Inventory.Respawntics 3024000 //1 hour
	Inventory.Amount 1
	Inventory.MaxAmount 3
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 3
	Inventory.Icon "ARWHUD"
	+NOGRAVITY
	+INVBAR
	-INVENTORY.INTERHUBSTRIP
	States
	{
	Spawn:
		ARWP A -1
		Stop
}}

Actor LoadedBolts2 : LoadedBolts
{
	+NOGRAVITY
	States
	{
	Spawn:
		ARWP A 350
		ARWP AAAAAAAAAAAAA 1 A_Gravity
		ARWP A -1
		Wait
}}
