//==================================================================
/////////////////////    MAGNUM   /////////////////////////////////
//=================================================================
Actor MagnumOnce : Inventory
{
	Inventory.MaxAmount 1
	-INVBAR
}

Actor FakeMagnum : CustomInventory //3010
{
	//$Category WDI Weapons
	//$Title "Magnum"
	Radius 16
	Height 8
	Inventory.PickupMessage "Found a 357 Magnum revolver."
	Inventory.Respawntics 3024000 //1 hour
	Scale 0.7
	States
	{
	Spawn:
		MAGN Z -1
		Stop
	Pickup:
		MAGN Z 0
		MAGN Z 0 A_JumpIfInventory("MurderMarker",1, 2)
		MAGN Z 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		MAGN Z 0
		MAGN Z 0 A_JumpIfInventory("MAGNUM",1,"AmmoPickup")
		MAGN Z 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		MAGN Z 0 A_PlaySound("Pickups/Weapon")
		MAGN Z 0 A_GiveInventory("MAGNUM",1)
		MAGN Z 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0
		MAGN Z 1 ACS_NamedExecute("OldWeapon_ID",0,21)
		MAGN Z 0 A_JumpIfInventory("MagnumOnce",1,"ForgetIt")
		MAGN Z 0 A_GiveInventory("MagnumAmmo",random(1,3))
		MAGN Z 0 A_GiveInventory("MagnumOnce",1)
		Stop
	NotPickup:
		MAGN Z 0
		MAGN Z 1 ACS_NamedExecute("OldWeapon_ID",0,21)
		Fail
	AmmoPickup:
		MAGN Z 0
		MAGN Z 0 A_JumpIfInventory("MagnumAmmo",7,"ForgetIt")
		MAGN Z 0 A_PlaySound("Pickups/Ammo")
		TNT1 A 0
		MAGN Z 1 A_GiveInventory("MagnumAmmo",random(1,8))
		Stop
	ForgetIt:
		MAGN Z 1
		Stop
}}

Actor Magnum : Weapon
{
	Obituary ""
	Radius 20
	Height 16
	Weapon.BobRangeX 0.7
	Weapon.BobRangeY 0.7
	Weapon.SelectionOrder 410
	Weapon.SlotNumber 1
	Weapon.Kickback 100
	Weapon.PreferredSkin "PistolSkin"
	Weapon.AmmoType "MagnumAmmo"
	Weapon.AmmoType2 "Speedloader"
	Weapon.AmmoUse 0
	Inventory.PickupMessage "You find a 357 Magnum Revolver."
	+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		MAGN Z -1
		Stop
	Ready:
		TNT1 A 0 A_PlaySound("MCOKING")
		MGMR IHGFEDCBA 1
	FakeReady:
		MAGN A 1 A_WeaponReady
		MAGN A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		loop
	WeaponDrop:
		TNT1 A 0
		TNT1 A 1 A_TakeInventory("IsDropping",1)
		TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		TNT1 A 0 A_FireCustomMissile("DroppedMagnum",0,0,0,0)
		TNT1 A 1 A_TakeInventory("Magnum",1)
		Stop
	Select:
		TNT1 A 0 A_Raise
	    wait
	Deselect:
		MAGN A 1 offset(0,42)
		MAGN A 1 offset(0,48)
		MAGN A 1 offset(0,55)
	    MAGN A 0 A_Lower
		MAGN A 1 A_Lower
		Goto Deselect +3
	Fire:
		TNT1 A 0 A_JumpIfInventory("MagnumAmmo",1,2)
		Goto EmptyAmmo
		MAGN ABC 1
		TNT1 A 0 A_TakeInventory("MagnumAmmo",1)
		MAGN C 1 Bright A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 4, 14, 0, 0)
		TNT1 A 0 A_FireBullets(1.5, 1.5, -1, 12, "FFBulletPuff")
		TNT1 A 0 A_Recoil(1)
		TNT1 A 0 A_PlaySound("DISCHARG",CHAN_WEAPON)
		MAGN DEFG 1
		MAGN G 2
		MAGN GFEDBA 1
		MAGN A 0 A_ReFire
		MAGN A 9
		Goto FakeReady
	Hold:
		TNT1 A 0 A_JumpIfInventory("MagnumAmmo",1,2)
		Goto EmptyAmmo
		SPMG AB 1
		//MAGN I 1
		SPMG CDEFGHIJ 1
		TNT1 A 0 Bright A_TakeInventory("MagnumAmmo",1)
		TNT1 A 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 4, 14, 0, 0)
		MAGN C 0 A_JumpIfCloser(120,"Closeshot")
		TNT1 A 0 A_FireBullets(2.1, 2.1, -1, 10, "FFBulletPuff")
		SPMG K 0 A_Recoil(1)
		TNT1 A 0 A_PlaySound("DISCHARG",CHAN_WEAPON)
		SPMG K 1  Bright
		SPMG LMNOPQR 1
		//MAGN I 1
		TNT1 A 0 A_ReFire
		SPMG BA 2
		Goto FakeReady
	EmptyAmmo:
		TNT1 AAA 0 A_PlaySound("HAMMER")
		MAGN AB 1 A_WeaponReady
		MAGN A 0
		Goto FakeReady
	Closeshot:
		TNT1 A 0 A_FireBullets(1.70, 1.70, -1, 9, "FFBulletPuff") //nerfed with distance added aim
		SPMG K 0 A_Recoil(1)
		TNT1 A 0 A_PlaySound("DISCHARG",CHAN_WEAPON)
		SPMG K 1  Bright
		SPMG LMNOPQR 1
		//MAGN I 1
		TNT1 A 0 A_ReFire
		SPMG BA 2
		Goto FakeReady	
	AltFire:
		TNT1 A 0 A_JumpIfInventory("MagnumAmmo",6,"FakeReady")
		TNT1 A 0 A_JumpIfInventory("Speedloader",1,"Reload")
		Goto FakeReady
	Reload:
		MAGN HI 1
		MAGN I 2
		MAGN IJKLM 2
		MAGN M 0 A_PlaySound("CYLOPEN")
		MAGN MN 1
		MAGN NO 1
		MAGN O 0 A_PlaySound("BTSOUT")
		MAGN OPQRSTU  2
		MAGN U 0 A_FireCustomMissile ("MagnumCaseSpawn",random(-4,4),0,1,1)
		MAGN U 1 A_FireCustomMissile ("MagnumCaseSpawn",random(-4,4),0,1,1)
		MAGN UVWXY 2
		MAGN Y 0 A_PlaySound("BTSIN")
		MAGN N 0 A_PlaySound("CYLCLOSE")
		//MAGN NM 2
		//MAGN MLKJI 1
		//MAGN IHGFBA 2
		MAGN NMMLKJI 2
		TNT1 A 0 A_PlaySound("MCOKING") 
		MAGN IHGFBA 2
		TNT1 A 0 A_JumpIfInventory("Speedloader",1,1)
		Goto EmptyAmmo
		TNT1 A 0 A_TakeInventory("Speedloader",1)
		TNT1 A 0 A_GiveInventory("MagnumAmmo",6)
		Goto FakeReady
}}

Actor MagnumCaseSpawn
{
	Speed 1
	Projectile
	+NOCLIP
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("PistolCasing",0,0,random(60,0),2,random(-20,15))
		TNT1 AA 1 A_CustomMissile("PistolCasing",0,0,random(60,0),2,random(-20,15))
		Stop
}}

Actor MagnumAmmo : Ammo
{
	Inventory.MaxAmount 6
	Inventory.Icon "PHURA0"
	+INVENTORY.UNDROPPABLE
}

Actor Speedloader : Ammo //3007
{
	//$Category WDI Ammo
	//$Title "SPLOAD"
	Scale 0.6
	Inventory.MaxAmount 3
	Inventory.Respawntics 21000 //10 minutes
	Inventory.Icon "ARTISPL"
	Inventory.PickupMessage "Picked up some speedloader Ammo"
	+INVENTORY.INVBAR
	-INVENTORY.INTERHUBSTRIP
	States
	{
	Spawn:
		SPMG	Z -1
		Stop
}}

Actor DroppedMagnum
{
	Radius 16
	Height 8
	Damage 0
	Speed 9
	Scale 0.7
	DeathSound "Weapons/Gundrop"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		MAGN Z 1
		Loop
	Death:
		MAGN Z 35
		MAGN Z 1 A_SpawnItemEx("FakeMagnum",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor Dropper_Magnum : DeathDropper
{
	States
	{
	Spawn:
		TNT1 A 1
		Wait
	Use:
		TNT1 A 1 A_FireCustomMissile("DroppedMagnum", 0, 0, 0)
		Stop
}}

Actor DeathDropper_Magnum : DeathDropper
{
	States
	{
	Spawn:
		TNT1 A 1
		Wait
	Use:
		TNT1 A 1 A_SpawnItemEx("FakeMagnum", 0, 0, 24.0, 0, 0, 0, 0,SXF_NOCHECKPOSITION)
		Stop
}}
