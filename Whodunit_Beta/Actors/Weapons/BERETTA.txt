//==================================================================
/////////////////////    BERETTA   /////////////////////////////////
//=================================================================
//Tweaks by Zedek
//Copied from OldBeretta
Actor FakeBeretta : CustomInventory Replaces Pistol 5010
{
	//$Category WDI Weapons
	//$Sprite PISTA0
	//$Title "Beretta"
	Radius 16
	Height 8
	Inventory.PickupMessage "Found a Beretta."
	Inventory.Respawntics 3024000 //1 hour
	Scale 0.7
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("MurderMarker",1, 2)
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0
		PIST A 0 A_JumpIfInventory("Beretta",1,"AmmoPickup")
		PIST A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		PIST A 0 A_PlaySound("Pickups/Weapon")
		PIST A 0 A_GiveInventory("Beretta",1)
		PIST A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Beretta.")
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		PIST A 0 A_JumpIfInventory("PistolOnce",1,"ForgetIt")
		PIST A 0 A_GiveInventory("PistolAmmo",random(1,8))
		PIST A 0 A_GiveInventory("PistolOnce",1)
		Stop
	NotPickup:
		PIST A 0
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		Fail
	AmmoPickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("PistolAmmo",7,"ForgetIt")
		PIST A 0 A_PlaySound("Pickups/Ammo")
		TNT1 A 0 //A_Print("Picked up some Pistol Ammo.")
		PIST A 1 A_GiveInventory("PistolAmmo",random(1,8))
		Stop
	ForgetIt:
		LUGP A 1
		Stop
}}

Actor TKFakeBeretta : FakeBeretta
{
	States
	{
	Pickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0 A_JumpIfInventory("Beretta",1,"AmmoPickup")
		PIST A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		PIST A 0 A_PlaySound("Pickups/Weapon")
		PIST A 0 A_GiveInventory("Beretta",1)
		PIST A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0 //A_Print("Picked up a Beretta.")
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		PIST A 0 A_JumpIfInventory("PistolOnce",1,"ForgetIt")
		PIST A 0 A_GiveInventory("PistolAmmo",random(1,8))
		PIST A 0 A_GiveInventory("PistolOnce",1)
		Stop
	NotPickup:
		PIST A 0
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		Fail
}}

Actor Beretta : Weapon
{
	Radius 20
	Height 16
	Scale 0.7
	Obituary ""
	//AttackSound "Weapons/BerettaFire"
	Weapon.PreferredSkin "PistolSkin"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 1
	Weapon.Kickback 100
	Weapon.AmmoType "PistolAmmo"
	Weapon.AmmoType2 "PistolClip"
	Weapon.AmmoUse 0
	Inventory.PickupMessage "You find an M1911." // "Picked up a pistol."
	+WEAPON.NOAUTOFIRE
	States
	{
	Ready:
		REPR A 0 A_PlaySound("Newpistol/Raise")
		REPS ABCDE 1
		REPA A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		REPA A 1 A_WeaponReady
		Goto Ready+6
	WeaponDrop:
		TNT1 A 0
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
		Goto Ready
	Deselect:
		REPS EDCBA 1
		TNT1 AAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Goto Deselect+5
	Select:
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",1,2)
		Goto ToReady+2
		TNT1 AAA 0
		BDPI B 0 Bright A_TakeInventory("PistolAmmo",1)
		BDPI B 0 A_JumpIfCloser(120,"Closeshot")
		REPR AA 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8,12, 0, 0)
		PISG B 0 A_FireBullets(1.75, 1.75, -1, 11, "FFBulletPuff")
		PISG B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 10, 0, 0)
		PISG B 0 A_PlaySound("Newpistol/Fire",CHAN_WEAPON)
		PISG B 0 A_AlertMonsters
		REPF A 1 Bright A_WeaponReady(WRF_NOFIRE)
		REPF BCD 1 A_WeaponReady(WRF_NOFIRE)
		REPF CE 1 A_WeaponReady(WRF_NOFIRE)
		REPA A 8 A_WeaponReady(WRF_NOFIRE)
		REPF E 0 A_ReFire
		Goto Ready+6
	ToReady:
		NULL A 0
		Goto Ready+6
		REPR AAA 0
		REPR A 0 A_PlaySound("DRYFIRE")
		Goto Ready+6
	AltFire:
		BDRI A 0 A_JumpIfInventory("PistolAmmo",10,"ToReady")
		BDRI A 0 A_JumpIfInventory("PistolClip",1,"Reload")
		Goto ToReady+2
	Reload:
		TNT1 AA 0
		REPR ABC 1
		REPR A 0 A_PlaySound("Newpistol/Clipout")
		REPR DEFGH 1
		REPR I 16
		REPR LMNOP 1
		REPR Q 0 A_PlaySound("Newpistol/Clipin")
		REPR QRST 2
		REPR U 0 A_PlaySound("Newpistol/Bolt")
		REPR U 2
		REPR V 5
		REPR WXYZ 1
		REPU ABCD 1
	CalcAmmo:
		REPR A 0 A_JumpIfInventory("PistolClip",1,2)
		Goto ToReady
		REPR AAA 0
		REPR A 0 A_TakeInventory("PistolClip",1)
		REPR A 0 A_GiveInventory("PistolAmmo",10)
		Goto ToReady
	Spawn:
		PIST A -1
		Stop
	Closeshot:
		REPR AA 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8, 12, 0, 0)
		PISG B 0 A_FireBullets(1.70, 1.70, -1, 9, "FFBulletPuff")  //nerfed with distance added aim
		PISG B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 12, 0, 0)
		PISG B 0 A_PlaySound("Newpistol/Fire",CHAN_WEAPON)
		PISG B 0 A_AlertMonsters
		REPF A 1 Bright A_WeaponReady(WRF_NOFIRE)
		REPF BCD 1 A_WeaponReady(WRF_NOFIRE)
		REPF CE 1 A_WeaponReady(WRF_NOFIRE)
		REPA A 8 A_WeaponReady(WRF_NOFIRE)
		REPF E 0 A_ReFire
		Goto Ready+6
}}

Actor DroppedBeretta
{
	Radius 16
	Height 8
	Damage 0
	Speed 9
	Scale 0.7
	DeathSound "Weapons/Gundrop"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		PIST A -1
		Stop
	Death:
		PIST A 35
		PIST A 1 A_SpawnItemEx("FakeBeretta",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor PistolOnce : Inventory
{
	Inventory.MaxAmount 1
	-INVBAR
}

Actor PistolAmmo : Ammo
{
	Inventory.MaxAmount 10
	Inventory.Icon "PHUDA0"
	+INVENTORY.UNDROPPABLE
}

Actor PistolClip : Ammo 30062
{
	//$Category WDI Ammo
	//$Sprite CLP4A0
	//$Title "Pistol Magazine"
	Scale 0.35
	Inventory.MaxAmount 2
	Inventory.Icon "ARTICLIP"
	Inventory.PickupMessage "Picked up some Pistol Ammo"
	Inventory.Respawntics 21000 //10 minutes
	+INVENTORY.INVBAR
	-INVENTORY.INTERHUBSTRIP
	States
	{
	Spawn:
		CLP4	A -1
		Stop
}}

Actor PisCaseSpawn     //Borrowed from the in-limbo ZNew mod (WildWeasel)
{
	Speed 15
	Projectile
	+NOCLIP
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("PistolCasing",0,0,random(-100,-80),2,random(-15,15))
		Stop
}}

Actor PistolSmokeSpawner
{
	Radius 1
	Height 1
	Speed 25
	Projectile
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_CustomMissile("PistolSmoke",-2,0,0)
		Stop
}}

Actor PistolSmoke
{
	Radius 1
	Height 1
	Scale .1
	Alpha .6
	RenderStyle Add
	+NOGRAVITY
	+NOBLOCKMAP
	+FLOORCLIP
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1
		Stop
}}

Actor PistolCasing
{
	Height 2
	Radius 2
	Mass 4
	Speed 10
	Scale 0.15
	BounceFactor 0.4
	SeeSound "Weapons/Casing1"
	Projectile
	+DOOMBOUNCE
	-NOGRAVITY
	-NOBLOCKMAP
	States
	{
	Spawn:
		SCAC A 0 A_PlaySound("NULL")
		SCAC ABCDE 2 A_CustomMissile("PisCaseSmoke",0,0,0,2,0)
		Goto Spawn+1
	Death:
		SCAC A 0 A_Jump(64,4)
		SCAC A 0 A_Jump(85,4)
		SCAC A 0 A_Jump(128,4)
		SCAC A 350
		Stop
		SCAC B 350
		Stop
		SCAC C 350
		Stop
		SCAC E 350
		Stop
}}

Actor PisCaseSmoke
{
	Radius 1
	Height 1
	Scale 0.125
	Speed 0
	Alpha 0.25
	RenderStyle Translucent
	Projectile
	+NOCLIP
	States
	{
	Spawn:
		SMKE ABCDEFGH 1
		Stop
}}

Actor SpentClip5 //pistol-Type clip
{
	Radius 3
	Height 3
	Scale 0.35
	Speed 0
	Mass 5
	DeathSound "Weapons/Smallclip"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	+FLOORCLIP
	States
	{
	Spawn:
		CLP4 ABCDEF 3
		Loop
	Death:
		CLP4 A 1000
		CLP4 AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.05)
		Stop
}}

//==================================================================
/////////////////////     OLD BERETTA   ////////////////////////////
//=================================================================
//Further tweaks by Prepper //(APOCALYPTO)
Actor FakeOldBeretta : CustomInventory 26906
{
	//$Category WDI Weapons
	//$Sprite PISTA0
	//$Title "OldBeretta"
	Radius 16
	Height 8
	Scale 0.7
	Inventory.PickupMessage "Found an old Beretta."
	Inventory.Respawntics 3024000
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("MurderMarker",1, 2)
		PIST A 0 A_JumpIfInventory("TKToken",1,"NotPickup")
		PIST A 0
		PIST A 0 A_JumpIfInventory("OldBeretta",1,"AmmoPickup")
		PIST A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		PIST A 0 A_PlaySound("Pickups/Weapon")
		PIST A 0 A_GiveInventory("OldBeretta",1)
		PIST A 0 A_GiveInventory("Slot1Filled",1)
		TNT1 A 0
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		PIST A 0 A_JumpIfInventory("PistolOnce",1,"ForgetIt")
		PIST A 0 A_GiveInventory("PistolAmmo",random(1,8))
		PIST A 0 A_GiveInventory("PistolOnce",1)
		Stop
	NotPickup:
		PIST A 0
		PIST A 1 ACS_ExecuteAlways(670,0,20)
		Fail
	AmmoPickup:
		PIST A 0
		PIST A 0 A_JumpIfInventory("PistolAmmo",7,"ForgetIt")
		PIST A 0 A_PlaySound("Pickups/Ammo")
		TNT1 A 0
		PIST A 1 A_GiveInventory("PistolAmmo",random(1,8))
		Stop
	ForgetIt:
		LUGP A 1
		Stop
}}

Actor OldBeretta : Weapon
{
	Radius 20
	Height 16
	Scale 0.7
	Obituary ""
	AttackSound "Weapons/BerettaFire"
	Inventory.PickupMessage "Picked up an old Beretta"
	Weapon.PreferredSkin "PistolSkin"
	Weapon.AmmoType "PistolAmmo"
	Weapon.AmmoType2 "PistolClip"
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 1
	Weapon.Kickback 100
	Weapon.AmmoUse 1
	+AMMO_OPTIONAL
	+ALT_AMMO_OPTIONAL
	+INVENTORY.UNDROPPABLE
	+NOAUTOAIM
	States
	{
	Spawn:
		PIST A -1
		Stop
	Ready:
		2PIS A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		2PIS A 0 A_JumpIfNoAmmo("EmptyReady")
		2PIS A 1 A_WeaponReady
		Goto Ready
	EmptyReady:
		2PIS A 0 A_JumpIfInventory("IsDropping",1,"WeaponDrop")
		2PIS C 3 A_WeaponReady
		Goto EmptyReady
	EmptyLul:
		2PIS C 1 A_PlaySound("Weapons/Lugerempty")
		2PIS C 11
		Goto EmptyReady
	Deselect:
		2PIS D 1 A_Lower
		Loop
	Select:
		2PIS A 1 A_Raise
		Loop
	Fire:
		2PIS A 0 A_JumpIfNoAmmo ("EmptyLul")
		2PIS A 4 A_JumpIfCloser(120,"Closeshot")
		2PIS B 2 Bright A_FireBullets(1.75, 1.75, -1, 11, "FFBulletPuff")
		2PIS B 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8, 12, 0, 0)
		2PIS B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 10, 0, 0)
		2PIS C 2 A_JumpIfNoAmmo ("EmptyLul")
		2PIS D 2
		2PIS A 2
		2PIS F 0 A_ReFire
		2PIS A 4 A_ReFire
		Goto Ready
	Closeshot:
		2PIS B 2 Bright A_FireBullets(1.70, 1.70, -1, 9, "FFBulletPuff")
		2PIS B 0 A_FireCustomMissile ("PistolSmokeSpawner", 0, 0, 8, 12, 0, 0)
		2PIS B 0 A_FireCustomMissile ("PisCaseSpawn", 0, 0, 8, 10, 0, 0)
		2PIS C 2 A_JumpIfNoAmmo ("EmptyLul")
		2PIS D 2
		2PIS A 2
		2PIS F 0 A_ReFire
		2PIS A 4 A_ReFire
		Goto Ready
	Reload:
		2PIS C 0 A_JumpIfInventory("PistolAmmo", 10, "Ready")
		2PIS C 0 A_TakeInventory("PistolClip", 1)
		2PIS C 0 A_PlaySound("Weapons/Lugerout")
		2PIS FGHIJK 2
		2PIS K 0 A_FireCustomMissile ("SpentClip5", 0, 0, 8, 12, 0, 0)
		2PIS K 24
		2PIS K 0 A_PlaySound("Weapons/Lugerin")
		2PIS LMNOPA 2
		2PIS Q 0 A_GiveInventory("PistolAmmo", 10)
		Goto Ready
	AltFire:
		Goto AltFire2
	AltFire2:
		2PIS A 0 A_JumpIfInventory("PistolClip", 1, "Reload")
		2PIS A 0 A_JumpIfNoAmmo(1)
		Goto Ready
		Goto EmptyReady
	WeaponDrop:
		TNT1 A 0
		2PIS A 1
		TNT1 A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(667,0,0,0,0)
		Goto Ready
}}

Actor DroppedOldBeretta
{
	Damage 0
	Radius 16
	Height 8
	Speed 9
	Scale 0.7
	DeathSound "Weapons/Gundrop"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		PIST A -1
		Stop
	Death:
		PIST A 35
		PIST A 1 A_SpawnItemEx("FakeOldBeretta",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor Dropper_OldBeretta : DeathDropper
{
	States
	{
	Spawn:
		TNT1 A 1
		Wait
	Use:
		TNT1 A 1 A_FireCustomMissile("DroppedOldBeretta", 0, 0, 0)
		Stop
}}

//deathdropper No included yet
