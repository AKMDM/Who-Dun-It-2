//==================================================================
/////////////////////      MOLOTOV     /////////////////////////////
//=================================================================
Actor FakeMolly : CustomInventory 30076
{
	//$Category WDI Weapons
	//$Sprite MOLVZ0
	//$Title "Molotov (Throwable)"
	Radius 16
	Height 8
	Scale 0.65
	Inventory.PickupMessage "Picked up a Molotov."
	Inventory.Respawntics 21000 //10 Minutes
	States
	{
	Spawn:
		MOLV Z -1
		Stop
	Pickup:
		MOLV Z 0
		MOLV Z 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		MOLV Z 0 A_PlaySound("Pickups/Weapon")
		MOLV Z 0 A_GiveInventory("Molotov",1)
		MOLV Z 0 A_GiveInventory("Slot1Filled",1)
		MOLV Z 1 ACS_NamedExecute("OldWeapon_ID",0,10)
		Stop
	NotPickup:
		MOLV Z 0
		MOLV Z 1 ACS_NamedExecute("OldWeapon_ID",0,10)
		Fail
}}

Actor Molotov : Weapon
{
	Radius 8
	Height 32
	Scale 0.65
	Inventory.PickupMessage "Picked up a Molotov."
	Weapon.SelectionOrder 2200
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOAUTOAIM
	States
	{
	Spawn:
		MOLV Z 1
		Goto Death
	Pickup:
	Deselect:
		MLTV F 5
		MLTV F 0 A_TakeInventory("MolotovLit",1)
		MLTV F 0 A_PlaySound("Weapons/Lighterclose",CHAN_WEAPON)
		MLTV E 4
		MLTV DCB 1
		MLTV A 0 A_Lower
		MLTV A 1 A_Lower
		Goto Deselect+7
	Select:
		MLTV A 1 A_Raise
		Loop
	Ready:
		MOLO A 0 A_JumpIfInventory("MolotovLit",1,"IdleReady")
		Goto LightPre
	LightPre:
		MLTV BCD 1
		MLTV E 4
		MLTF E 0 A_PlaySound("Weapons/Lighteropen",CHAN_WEAPON)
		Goto Lighter
	Lighter:
		MLTV F 9
		MLTV G 0 A_JumpIfInventory("Flicked",3,"Success")
		MLTV G 0 A_Jump(64,"Success")
		MLTV G 0 A_PlaySound("Weapons/Lighter",CHAN_WEAPON)
		MLTV G 0 A_GiveInventory("Flicked",1)
		MLTV G 4
		Loop
	Success:
		MLTV G 0 A_TakeInventory("Flicked",3)
		MLTV G 0 A_GiveInventory("MolotovLit",1)
		MLTV G 0 A_PlaySound("Weapons/Lighter",CHAN_WEAPON)
		MLTV G 2
		MLTV Z 2
		Goto IdleReady
	IdleReady:
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV I 2 Bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV J 2 Bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MLTV K 2 Bright A_WeaponReady
		Loop
	DropWeapon:
		MOLO A 0 A_TakeInventory("MolotovLit",1)
		MLTV A 0
		MOLO A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
		Goto Ready
	Fire:
		MLTV L 2 Bright
		MLTV MNM 2 Bright
		MLTV O 0 A_TakeInventory("MolotovLit",1)
		MLTV O 0 A_PlaySound("Weapons/Lighterclose",CHAN_WEAPON)
		MLTV OPQR 1
		MLTV L 0 A_ReFire
		TNT1 A 2
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Molotov",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		MOLO A 0
		Goto Ready
	Hold:
		TNT1 A 2
		TNT1 A 1 A_ReFire
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Molotov",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		MOLO A 0
		Stop
}}

Actor DroppedMolly
{
	Radius 16
	Height 8
	Damage 0
	Scale 0.65
	Speed 9
	DeathSound "Molly/Land"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		MOLV Z -1
		Stop
	Death:
		MOLV Z 35
		MOLV Z 1 A_SpawnItemEx("FakeMolly",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

//==================================================================
/////////////////////    OLD MOLOTOV   /////////////////////////////
//=================================================================
//Molotov (Throwing Weapon) || Sprites by Lightman and Mooseknuckle, code by Theshooter7
Actor FakeOldMolly : CustomInventory //30076 it needs a new id
{
	//$Category WDI Weapons
	//$Sprite MOLPA0
	//$Title "OldMolotov (Throwable)"
	Radius 16
	Height 8
	Scale 0.45
	Inventory.PickupMessage "Picked up a Molotov."
	Inventory.Respawntics 21000 //10 Minutes
	States
	{
	Spawn:
		MOLP A -1
		Stop
	Pickup:
		MOLP A 0
		MOLP A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		MOLP A 0 A_PlaySound("Pickups/Weapon")
		MOLP A 0 A_GiveInventory("OldMolotov",1)
		MOLP A 0 A_GiveInventory("Slot1Filled",1)
		MOLP A 1 ACS_NamedExecute("OldWeapon_ID",0,10)
		Stop
	NotPickup:
		MOLP A 0
		MOLP A 1 ACS_NamedExecute("OldWeapon_ID",0,10)
		Fail
}}

Actor OldMolotov : Weapon
{
	Radius 8
	Height 32
	Scale 0.45
	Inventory.PickupMessage "Picked up a Molotov."
	Weapon.SelectionOrder 2200
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOAUTOAIM
	States
	{
	Spawn:
		MOLP A 1
		Goto Death
	Pickup:
	Deselect:
		MOLO AAAA 1 Bright A_Lower
		MOLO BBBB 1 Bright A_Lower
		MOLO CCCC 1 Bright A_Lower
		Loop
	Select:
		TNT1 A 3
		TNT1 A 15 A_PlaySound("Weapons/Lighteropen")
		TNT1 A 20 A_PlayWeaponSound("Weapons/Lighter")
		TNT1 A 5 A_JumpIf(random(0,10) > 8, "SelectPre")
		TNT1 A 20 A_PlayWeaponSound("Weapons/Lighter")
		TNT1 A 5 A_JumpIf(random(0,10) > 6, "SelectPre")
		TNT1 A 20 A_PlayWeaponSound("Weapons/Lighter")
		TNT1 A 5
	SelectPre:
		TNT1 A 5 A_PlaySound("Weapons/Lighterclose")
	SelectLoop:
		MOLO AAAA 1 Bright A_Raise
		MOLO BBBB 1 Bright A_Raise
		MOLO CCCC 1 Bright A_Raise
		Goto SelectLoop
	Ready:
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO A 1 Bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO A 1 Bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO A 1 Bright A_WeaponReady
		MOLO A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO A 1 Bright A_WeaponReady
		MOLO B 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO B 1 Bright A_WeaponReady
		MOLO B 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO B 1 Bright A_WeaponReady
		MOLO B 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO B 1 Bright A_WeaponReady
		MOLO B 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO B 1 Bright A_WeaponReady
		MOLO C 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO C 1 Bright A_WeaponReady
		MOLO C 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO C 1 Bright A_WeaponReady
		MOLO C 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO C 1 Bright A_WeaponReady
		MOLO C 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		MOLO C 1 Bright A_WeaponReady
		Loop
	DropWeapon:
		TNT1 A 0
		TNT1 A 1 A_TakeInventory("IsDropping",1)
		TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		TNT1 A 0 A_FireCustomMissile("DroppedOldMolly",0,0,0,0)
		TNT1 A 1 A_TakeInventory("OldMolotov",1)
		Stop
	Fire:
		MOLO DEFGHIJ 1 Bright
		TNT1 A 2
		TNT1 A 1 A_ReFire
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		TNT1 A 0 A_TakeInventory("OldMolotov",1)
		TNT1 A 0 //ACS_ExecuteAlways(653, 0, 0, 0, 0) <----useless
		TNT1 A 2
		MOLO A 0
		Goto Ready
	Hold:
		TNT1 A 2
		TNT1 A 1 A_ReFire
		THRW I 2 A_FireCustomMissile("ThrownMolly", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		TNT1 A 0 A_TakeInventory("OldMolotov",1)
		TNT1 A 0 //ACS_ExecuteAlways(653, 0, 0, 0, 0) <----useless
		TNT1 A 2
		MOLO A 0
		Stop
}}

Actor DroppedOldMolly
{
	Radius 16
	Height 8
	Damage 0
	Scale 0.45
	Speed 9
	DeathSound "Molly/Land"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		MOLP A -1
		Stop
	Death:
		MOLP A 35
		MOLP A 1 A_SpawnItemEx("FakeOldMolly",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor Dropper_OldMolly : DeathDropper
{
	States
	{
	Spawn:
		TNT1 A 1
		Wait
	Use:
		TNT1 A 1 A_FireCustomMissile("DroppedOldMolly", 0, 0, 0)
		Stop
}}

//deathdropper No included yet
//----------------------------------------------------------------------------------
Actor ThrownMolly
{
	Radius 6
	Height 4
	Speed 20
	Damage 0
	Scale 0.45
	Gravity 0.85
	DamageType "Friendlythrown"
	SeeSound "Molly/Flame"
	DeathSound "Molly/Burst"
	Obituary ""
	Projectile
	+NOBLOOD
	+SKYEXPLODE
	-NOGRAVITY
	-BOUNCEONACTORS
	-BLOODSPLATTER
	-EXPLODEONWATER
	States
	{
	Spawn:
		MOPP ABCDEFGHIJKLMNOP 2 Bright
		Loop
	Death:
		TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
		TNT1 A 0 A_Explode(10, 64, 1)
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MollyShard",1,0,random(0,360), 2, random(5, 45))
		TNT1 AAAAAAAAAAAAA 0 A_CustomMissile("MollyFireSpawn",1,0,random(0,360), 2, random(5, 30))
		TNT1 AAAA 0 A_CustomMissile("MollyFireSpawn3",1,0,random(0,360), 2, random(5, 30))
		TNT1 AAA 0 A_CustomMissile("MollyFireSpawn2",1,0,random(0,360), 2, random(30, 70))
		TNT1 A 1
		Stop
	InWater:
		TNT1 A 1
		Stop
}}

Actor MollyFireSpawn
{
	Radius 3
	Height 4
	Speed 9
	Damage 0
	Scale 0.8
	Gravity 0.9
	Alpha 1.0
	RenderStyle Add
	DamageType "Burn"
	Projectile
	+RIPPER
	+FLOORHUGGER
	//+CEILINGHUGGER
	+DONTSPLASH
	+NOBLOOD
	+NODAMAGETHRUST
	-NOGRAVITY
	-BLOODSPLATTER
	States
	{
	Spawn:
		TNT1 A 6
		Goto Death
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor MollyFireSpawn2 : MollyFireSpawn
{
	Speed 25
	Gravity 0.8
	-FLOORHUGGER
	States
	{
	Spawn:
		CFFX CDEFGHI 2 Bright
		Goto Death
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor MollyFireSpawn3 : MollyFireSpawn
{
	States
	{
	Spawn:
		TNT1 A 3
		Goto Death
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 1 A_SpawnItemEx("MollyFire", 0, 0, 0, 0, 0, 0, 0,SXF_NOCHECKPOSITION)
		Stop
}}

Actor MollyFire
{
	Health 15
	Radius 3
	Height 4
	Speed 8
	Damage 0
	Scale 0.8
	Gravity 1.5
	ReactionTime 60 // About 1 minute
	Alpha 0.0
	RenderStyle Add
	DamageFactor "Ice", 0.5
	DamageFactor  "None",0.0
	ActiveSound "Molly/Burn"
	DamageType "Burn"
	Projectile
	+SHOOTABLE
	+BLOODLESSIMPACT
	+DONTSPLASH
	+SLIDESONWALLS
	+FLOORHUGGER
	+RIPPER
	+NOBLOOD
	+NODAMAGETHRUST
	-NOGRAVITY
	-BLOODSPLATTER
	-NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
		TNT1 A 0 A_PlaySound("Molly/Burn",CHAN_VOICE, 1.0, 1)
		CFFX CDEFGHICDE 3 Bright A_FadeIn(0.1)
		Goto Ignited
	Ignited:
		TNT1 A 0 A_JumpIf(waterlevel >= 2, "InWater")
		TNT1 A 0 A_PlaySound("Molly/Burn",CHAN_VOICE, 1.0, 1)
		CFFX CDEFGHICDEF 3 Bright A_Explode(2, 64, 1)
		TNT1 A 0 A_CountDown
		Loop
	Death:
		TNT1 A 0 A_StopSound(CHAN_AUTO)
		CFFX CDEFGHICDEF 3 Bright A_FadeOut(0.1)
		Wait
	InWater:
		TNT1 A 1
		Stop
}}

Actor MollyShard
{
	Radius 6
	Height 4
	Speed 20
	Damage 0
	BounceCount 2
	Scale 0.5
	Gravity 0.95
	Alpha 0.75
	RenderStyle Add
	Projectile
	+DONTSPLASH
	+DOOMBOUNCE
	+RIPPER
	+NOBLOOD
	-NOGRAVITY
	-BLOODSPLATTER
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Shard1", "Shard2", "Shard3", "Shard4")
		TNT1 A 1
		Stop
	Shard1:
		ICEC A 350
		ICEC A 35 A_FadeOut(0.05)
		Wait
	Shard2:
		ICEC B 350
		ICEC B 35 A_FadeOut(0.05)
		Wait
	Shard3:
		ICEC C 350
		ICEC C 35 A_FadeOut(0.05)
		Wait
	Shard4:
		ICEC D 350
		ICEC D 35 A_FadeOut(0.05)
		Wait
	Death:
		TNT1 A 0 A_Jump(256, "Shard1", "Shard2", "Shard3", "Shard4")
		TNT1 A 1
		Stop
}}

Actor MolotovLit : Inventory
{
	Inventory.MaxAmount 1
}

Actor Flicked : Inventory
{
	Inventory.MaxAmount 3
}
