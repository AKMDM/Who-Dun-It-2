//==================================================================
/////////////////////    BRICK   ///////////////////////////////////
//=================================================================
//Brick (Throwing Weapon) || Sprites by Mooseknuckle, code by Theshooter7
Actor FakeBrick : CustomInventory 30075
{
	//$Category WDI Weapons
	//$Sprite BRKPU0
	//$Title "Brick (Throwable)"
	Radius 16
	Height 8
	Inventory.PickupMessage "Picked up a dry brick. Stolen from Sicamore's house."
	Inventory.Respawntics 10500 //5 Minutes
	States
	{
	Spawn:
		BRKP U -1
		Stop
	Pickup:
		BRKP U 0
		BRKP U 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		BRKP U 0 A_PlaySound("Pickups/Weapon")
		BRKP U 0 A_GiveInventory("Brick",1)
		BRKP U 0 A_GiveInventory("Slot1Filled",1)
		BRKP U 1 ACS_ExecuteAlways(670,0,24)
		Stop
	NotPickup:
		BRKP U 0
		BRKP U 1 ACS_ExecuteAlways(670,0,24)
		Fail
}}

Actor Brick : Weapon
{
	Radius 8
	Height 32
	Weapon.SelectionOrder 2200
	Inventory.PickupMessage "Picked up a dry brick. Good for throwing!"
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOAUTOAIM
	States
	{
	Spawn:
		BRKP U 0
		Goto Death
	Pickup:
	Select:
		BRIK A 0 A_Raise
		BRIK A 0 A_Raise
		BRIK A 1 A_Raise
		Loop
	Deselect:
		BRIK A 0 A_Lower
		BRIK A 0 A_Lower
		BRIK A 1 A_Lower
		Loop
	Ready:
		BRIK A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		BRIK A 1 A_WeaponReady
		Loop
	DropWeapon:
		BRIK A 0
		BRIK A 1
		BRIK A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
		Goto Ready
	Fire:
		BRIK BCDEF 1
		TNT1 A 2
		TNT1 A 1 A_ReFire
		THRW I 2 A_FireCustomMissile("ThrownBrick", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Brick",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		BRIK A 0
		Goto Ready
	Hold:
		TNT1 A 1
		TNT1 A 1 A_ReFire
		THRW I 2 A_FireCustomMissile("ThrownBrick", 0, 0, 6, 10, 0, 8)
		THRW JK 2
		THRW LMNOPQ 2
		TNT1 A 4
		//TNT1 A 0 A_TakeInventory("Slot1Filled",1)
		//TNT1 A 0 A_TakeInventory("Brick",1)
		TNT1 A 0 ACS_ExecuteAlways(653, 0, 0, 0, 0)
		TNT1 A 2
		BRIK A 0
		Stop
}}

Actor ThrownBrick
{
	Radius 6
	Height 4
	Speed 30
	Gravity 0.8
	Damage (27)
	BounceCount 3
	BounceFactor 0.2
	DamageType "Friendlythrown"
	//SeeSound "Shovel/Hit"
	SeeSound "Brick/Land"
	AttackSound "Shovel/Hit"
	DeathSound "Brick/Land"
	//BounceSound "Brick/Land"
	Obituary ""
	Projectile
	-NOGRAVITY
	+DOOMBOUNCE
	//+BOUNCEONACTORS
	+BLOODSPLATTER
	+EXPLODEONWATER
	+SKYEXPLODE
	States
	{
	Spawn:
		BRKT IGFEDCBA 2
		Loop
	Death:
		BRKP U 1 A_SpawnItem("FakeBrick",0,0,0,0)
		Stop
}}

Actor DroppedBrick
{
	Radius 16
	Height 8
	Damage 0
	Speed 9
	DeathSound "Brick/Land"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		BRKP U -1
		Stop
	Death:
		BRKP U 35
		BRKP U 1 A_SpawnItemEx("FakeBrick",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
}}
