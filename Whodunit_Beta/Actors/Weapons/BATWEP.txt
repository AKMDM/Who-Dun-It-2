//==================================================================
/////////////////////     BASEBALLBAT  /////////////////////////////
//=================================================================
Actor FakeBat : CustomInventory 22473
{
	//$Category WDI Weapons
	//$Title "Bat"
	Radius 16
	Height 8
	Scale 0.55
	Inventory.PickupMessage "Found a baseball bat"
	Inventory.Respawntics 3150 //1 minute 30 seconds
	States
	{
	Spawn:
		BBAT A -1
		Stop
	Pickup:
		BBAT A 0
		BBAT A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		BBAT A 0 A_PlaySound("Pickups/Weapon")
		BBAT A 0 A_GiveInventory("BaseballBat",1)
		BBAT A 0 A_GiveInventory("Slot1Filled",1)
		BBAT A 1 ACS_NamedExecute("OldWeapon_ID",0,14)
		Stop
	NotPickup:
		BBAT A 0
		BBAT A 1 ACS_NamedExecute("OldWeapon_ID",0,14)
		Fail
}}

Actor FakeBatY : CustomInventory 22474
{
	//$Category WDI Weapons
	//$Title "Bat (Standing)"
	Radius 8
	Height 16
	Scale 0.55
	Inventory.PickupMessage "Found a baseball bat"
	Inventory.Respawntics 3150 //1 minute 30 seconds
	States
	{
	Spawn:
		BBYT A -1
		Stop
	Pickup:
		BBYT A 0
		BBYT A 0 A_JumpIfInventory("Slot1Filled",1,"NotPickup")
		BBYT A 0 A_PlaySound("Pickups/Weapon")
		BBYT A 0 A_GiveInventory("BaseballBat",1)
		BBYT A 0 A_GiveInventory("Slot1Filled",1)
		BBYT A 1 ACS_NamedExecute("OldWeapon_ID",0,14)
		Stop
	NotPickup:
		BBYT A 0
		BBYT A 1 ACS_NamedExecute("OldWeapon_ID",0,14)
		Fail
}}

Actor BaseballBat : Weapon
{
	Radius 8
	Height 32
	Scale 0.2
	Inventory.PickupMessage "Found a baseball bat."
	Weapon.SelectionOrder 2200
	+WEAPON.MELEEWEAPON
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INTERHUBSTRIP
	States
	{
	Spawn:
		BBAT A 0
		Goto Death
	Pickup:
	Select:
		BSBT A 1 A_Raise
		Loop
	Deselect:
		BSBT A 1 A_Lower
		Loop
	Ready:
		BSBT A 0 A_JumpIfInventory("IsDropping",1,"DropWeapon")
		BSBT A 1 A_WeaponReady
		Loop
	DropWeapon:
		BSBT A 0
		BSBT A 1
		BSBT A 0 A_TakeInventory("IsDropping",1)
		TNT1 A 0 ACS_ExecuteAlways(418,0,0,0,0)
		Goto Ready
	Fire:
		BSBT A 0 A_JumpIfInventory("PipeSeq",2,"Fire3")
		BSBT A 0 A_JumpIfInventory("PipeSeq",1,"Fire2")
		BSBT ABC 1
		TNT1 A 1 A_ReFire
		TNT1 A 1 A_GiveInventory("PipeSeq",1)
		BSBT DE 2
		BSBT F 1 Offset(19,64)
		BSBT F 0 Offset(6,83)
		BSBT G 1 A_CustomPunch(20,1,0,"BatPuff", 75)
		BSBT G 0 Offset(-15,69)
		BSBT H 1 Offset(5,70)
		BSBT I 0 Offset(-4,48)
		BSBT I 1 Offset(-8,55)
		BSBT I 0
		BSBT I 1 Offset(-19,94)
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady
		BSBT C 1 A_TakeInventory("PipeSeq",999)
		BSBT BA 1
		Goto Ready
	Hold:
		TNT1 A 1
		TNT1 A 1 A_ReFire
		TNT1 A 6 A_GiveInventory("PipeSeq",1)
		BSBT DE 1
		BSBT F 1 Offset(19,64)
		BSBT F 0 Offset(6,83)
		BSBT G 1 A_CustomPunch(25,1,0,"BatPuff", 75)
		BSBT G 0 Offset(-15,69)
		BSBT H 1 Offset(5,70)
		BSBT I 0 Offset(-4,48)
		BSBT I 1 Offset(-8,55)
		BSBT I 0
		BSBT I 1 Offset(-19,94)
		TNT1 A 2
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady
		BSBT C 1 A_TakeInventory("PipeSeq",999)
		BSBT BA 1
		Goto Ready
	Fire2:
		TNT1 A 10 A_GiveInventory("PipeSeq",1)
		BSBT JKL 1
		BSBT M 1 A_CustomPunch(random(25,30),1,0,"BatPuff", 75)
		BSBT No 1
		BSBT A 8
		BSBT AAAAAAAAAAAA 1 A_WeaponReady
		BSBT C 1 A_TakeInventory("PipeSeq",999)
		BSBT BA 1
		Goto Ready
	Fire3:
		TNT1 A 10 A_TakeInventory("PipeSeq",1)
		BSBT DE 1
		BSBT F 1 Offset(19,64)
		BSBT F 0 Offset(6,83)
		BSBT G 1 A_CustomPunch(35,1,0,"BatPuff", 75)
		BSBT G 0 Offset(-15,69)
		BSBT H 1 Offset(5,70)
		BSBT I 0 Offset(-4,48)
		BSBT I 1 Offset(-8,55)
		BSBT I 0
		BSBT I 1 Offset(-19,94)
		BSBT C 1 A_TakeInventory("PipeSeq",999)
		BSBT BA 1
		Goto Ready
	Death:
		BBAT A 0 A_SpawnItem("DroppedBat",0,0,0,0)
		TNT1 A 0 A_FadeOut(1.0)
		Goto Death + 1
}}

Actor BatPuff
{
	DamageType "Friendlymelee"
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	ActiveSound "Cue/Swing"
	AttackSound "Bat/Hitwall"
	SeeSound "Bat/Hitwall"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
		Stop
	Crash:
		TNT1 A 0
		POOF ABCDE 2
		Stop
	Melee:
		TNT1 A 0
		POOF ABCDE 2
		Stop
}}

Actor DroppedBat
{
	Radius 16
	Height 8
	Damage 0
	Speed 9
	Scale 0.55
	DeathSound "Bat/Hitwall"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		BBAT A -1
		Stop
	Death:
		BBAT A 35
		BBAT A 1 A_SpawnItem("FakeBat",0,0,0,0)
		Stop
}}
