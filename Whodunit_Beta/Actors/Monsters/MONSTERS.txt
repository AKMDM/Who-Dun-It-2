//==================================================================
/////////////////////    MONSTERS    //////////////////////////////
//=================================================================
Actor Rat 28674
{
	//$Category WDI Creatures
	//$Sprite RATTA2A8
	//$Title "Rat (Gives Sanity on Death)"
	Health 10
	MaxStepHeight 4
	Radius 4
	Height 6
	Speed 13
	DamageFactor "Gas", 0.0
	DamageFactor "FlareEffect", 0.0
	ActiveSound "Rat/Squeak"
	DeathSound "Rat/Die"
	Monster
	+ALWAYSRESPAWN
	+DONTFALL
	+PUSHABLE
	+NODROPOFF
	+DONTSPLASH
	-ISMONSTER
	-TELESTOMP
	-CANPASS
	-CANUSEWALLS
	-COUNTKILL
	-CANPUSHWALLS
	States
	{
	Spawn:
		RATT A 1 A_Chase
		RATT A 8 A_Jump(60, "Walk", "TryAgain")
		Loop
	TryAgain:
		RATT B 8 A_Jump(60, "Walk", "Spawn")
		Loop
	Walk:
		RATT ABABAB 4 A_Wander
		RATT A 0 A_JumpIfCloser(64, "Scared")
		RATT A 0 A_CheckSight("Spawn")
		Loop
	Scared:
		RATT A 1 A_ChangeFlag("FRIGHTENED", 1)
		RATT ABABABAB 3 A_Chase
		RATT A 0 A_CheckSight("Spawn")
		Loop
	Death:
		RATT A 0 A_GiveToTarget("MadMeter", 40)
		RATT A 1 A_PlaySound("Rat/Die")
		MEAT R 4 A_Burst("RatMeat")
		MEAT QP 4
		MEAT P -1
		Stop
}}

Actor RatMeat {
	Scale 0.3
	Gravity 0.4
	BounceCount 5
	ReactionTime 20
	+CLIENTSIDEONLY
	+HEXENBOUNCE
	+DONTSPLASH
	States {
	Spawn:
		MEAT S 1 A_Jump(150, "Two")
	One:
		MEAT S 3 A_CountDown
		Loop
	Two:
		MEAT T 3 A_CountDown
		Loop
	Death:
		Stop
}}

/* sprites: remove?? any Use for this Monster after 24 maps
Actor Gonefishing 14254
{
Speed 24
MaxStepHeight 16
MaxDropOffHeight 32
Monster
+FRIENDLY
-SHOOTABLE
//$Category WDI Easter Eggs
//$Sprite GNFIA2A8
//$Title "GONE FISHING"
States
{
Spawn:
GNFI A 1 A_Look2
Loop
See:
GNFI ABCD 4 A_Wander
Loop
}}
*/
Actor Netherdemon 30010
{
	//$Category WDI Creatures
	Radius 18
	Height 59
	Health 166
	Mass 0x7fffffff
	PainChance 180
	Speed 13
	Scale .90
	MeleeDamage 3
	MeleeRange 66
	DamageFactor "Gas", 0.0
	DamageFactor "FlareEffect", 0.0
	Obituary "%o was slain"
	Monster
	+FLOORCLIP
	+FASTER
	+NOGRAVITY
	+FLOAT
	States
	{
	Spawn:
		NTDM A 2 A_Look
		NTDM B 2 A_Look
		NTDM C 1 A_Look
		NTDM D 2 A_Look
		NTDM E 2 A_Look
		Loop
	See:
		NTDM A 1 A_Chase
		NTDM B 1 A_Chase
		NTDM C 1 A_Chase
		NTDM D 1 A_Chase
		NTDM E 1 A_Chase
		Loop
	Melee:
		NTDM F 5 A_FaceTarget
		NTDM F 1 A_MeleeAttack
		NTDM F 5 A_FaceTarget
		NTDM A 2 A_FaceTarget
		Goto See
	Pain:
		NTDM R 6 A_Pain
		Goto See
	Death:
		NTDM F 320 A_PlaySound("CVOICES")
		NTDM G 70 A_Scream
		NTDM H 2
		NTDM I 2
		NTDM J 2
		NTDM K 2
		NTDM L 2
		NTDM M 2 A_SpawnItemEx("Netheregg")
		NTDM N 2 A_NoBlocking
		Stop
}}

Actor Trite 17121 ////////////////////from Shadow Mavericks
{
	//$Category Marsis_map_ONLY
	SpawnID 251
	Radius 12
	Height 16
	Health 10
	Scale 0.4
	Damage 2
	Speed 10
	PainChance 255
	Gravity 0.7
	MinMissileChance 116
	MaxTargetRange 128
	PoisonDamage 3
	DamageFactor "FlareEffect", 0.0
	DamageType "Trite"
	DamageFactor "Trite", 0
	HitObituary "Someone didn't pay attention."
	Obituary "'"
	BloodColor "00 FF 00"
	SeeSound "Tritesee"
	DeathSound "Tritedeath"
	PainSound "Tritepain"
	Monster
	+DROPOFF
	+LOOKALLAROUND
	States
	{
	Spawn:
		TRIT A 10 A_Look
		Loop
	See:
		TRIT ABCD 3 A_Chase
		TRIT A 0 A_PlaySound("TriteStep")
		Loop
	Missile:
		TRIT E 8 A_FaceTarget
		TRIT E 0 A_PlaySound("Triteattack")
		TRIT E 0 A_Recoil(-18)
		TRIT E 0 ThrustThingZ(0, 4, 0, 0)
		TRIT FFFFFF 3 A_CustomBulletAttack (4, 1, 1, 1, "TriteHit", 32)
		TRIT E 4
		Goto See
	Pain:
		TRIT E 2
		TRIT F 2 A_Pain
		Goto See
	Death:
		TRIT G 0 A_Scream
		TRIT G 0 A_Fall
		TRIT GHIJ 4
		TRIT J -1
		Stop
	Disabled:
		TNT1 A 0
		TNT1 A 0 A_JumpIfHealthLower(1, "Death")
		TNT1 A 1 A_ChangeFlag("SOLID", 0)
		TNT1 A 1 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 1 A_SetTranslucent(0.0, 0)
		TNT1 A -1
		Loop
	Enabled:
		TNT1 A 0
		TNT1 A 0 A_JumpIfHealthLower(1, "Death")
		TNT1 A 1 A_ChangeFlag("SOLID", 1)
		TNT1 A 1 A_ChangeFlag("SHOOTABLE", 1)
		TNT1 A 1 A_SetTranslucent(1.0, 0)
		Goto Spawn
}}

Actor TriteHit : BulletPuff
{
	PoisonDamage 2
	DamageType "Trite"
	+BLOODLESSIMPACT
	+NOBLOOD
	+PUFFONACTORS
	-BLOODSPLATTER
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("TriteBite")
		Stop
}}

Actor Fish1 24221
{
	//$Category WDI Creatures
	Radius 24
	Height 48
	Speed 4
	Scale 0.2
	+SOLID
	+CANPASS
	+FLOAT
	+NOGRAVITY
	States
	{
	Spawn:
		FSH1 A 2 A_Wander
		Loop
}}

Actor Fish1Small : Fish1 24222
{
	Radius 16
	Height 32
	Speed 2
	Scale 0.15
	States
	{
	Spawn:
		FSH1 A 2 A_Wander
		Loop
}}

Actor Fish2 : Fish1 24223
{
	States
	{
	Spawn:
		FSH2 A 2 A_Wander
		Loop
}}

Actor Fish3 : Fish1 24224
{
	States
	{
	Spawn:
		FSH3 A 2 A_Wander
		Loop
}}

Actor Fish4Small : Fish1Small 24225
{
	Speed 4
	States
	{
	Spawn:
		FSH4 A 2 A_Wander
		Loop
}}